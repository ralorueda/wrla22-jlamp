mod IDEMPOTENCE-VENDING-MACHINE is
   sorts Coin Item Marking Money State .
   subsort Coin < Money .
   subsort Money Item < Marking .

   op empty : -> Money .
   op __ : Money Money -> Money [assoc comm id: empty] .
   op __ : Marking Marking -> Marking [assoc comm id: empty] .
   op <_> : Marking -> State .
   ops $ q : -> Coin .
   ops c a : -> Item .

   var M : Marking .   var I : Item .

   rl [buy-c] : < M $ > => < M c > [narrowing] .
   rl [buy-a] : < M $ > => < M a q > [narrowing] .
   eq [change] : q q q q M = $ M [variant] .
   eq [idem-dollar] : $ $ M = $ M [variant] .
   eq [idem-item-a] : a a M = a M [variant] .
   eq [idem-item-c] : c c M = c M [variant] .
endm
